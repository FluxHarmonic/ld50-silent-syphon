(define-module (syphon main)
  (import (mesche array)
          (mesche math)
          (mesche list)
          (mesche time)
          (mesche string)
          (substratic font)
          (substratic input)
          (substratic engine)
          (substratic window)
          (substratic physics)
          (substratic texture)
          (substratic renderer)
          (syphon enemy)
          (syphon input)
          (syphon player)
          (syphon weapon)
          (syphon bullet)
          (syphon collision)))

(define window (window-create 1280 720 "Silent Syphon"))
(define input-state (input-init window))
(define renderer (renderer-create window))

;; Load game assets
(define small-font (font-load-file "assets/fonts/Thintel.ttf" 75))
(define result-font (font-load-file "assets/fonts/Thintel.ttf" 250))
(define prompt-font (font-load-file "assets/fonts/Thintel.ttf" 125))

(define screen-scale 4.0)

;; Show the window
(window-show window)

;; Get an fps counter
(define current-fps (make-fps-counter))

;;; -- Game --

(define score-scroll-speed 150)
(define max-level-time 180)

(define-record-type game
  (fields state
          player
          controller
          score-total
          score-current
          level-time
          enemies
          bullets))

(define (initialize-game)
  (make-game :state 'playing
             :player (create-player window)
             :controller (create-controller)
             :score-total 0
             :score-current 0
             :level-time 0
             :enemies '()
             :bullets '()))

(define (end-game game state)
  (game-state-set! game state)
  (if (equal? state 'died)
      (begin
        (set! result-text you-died-text)
        (set! result-width you-died-width))
      (begin
        (set! result-text depleted-text)
        (set! result-width depleted-width))))

;;; -- HUD --

(define hud-width 250)
(define meter-height 300)
(define hud-color (rgba 92 87 85 90))
(define shield-meter-color (rgb 158 252 255))
(define energy-meter-color (rgb 255 105 41))

(define (update-hud game time-delta)
  (if (< (game-score-current game)
         (game-score-total game))
      (game-score-current-set! game
                               (min (game-score-total game)
                                    (+ (game-score-current game)
                                       (* score-scroll-speed time-delta))))))

(define (render-hud renderer game player window)
  (renderer-draw-rect renderer
                      (- (window-width window) hud-width)
                      (- (window-height window) 520)
                      (window-width window)
                      (window-height window)
                      hud-color)


  (let ((shield-meter-height (* (/ (player-shield-amount player) 100) meter-height))
        (shield-meter-x (- (window-width window) 190)))

    (render-text renderer
                 small-font
                 "S"
                 (+ shield-meter-x 11)
                 (- (- (window-height window) 70)
                    meter-height))
    (renderer-draw-rect renderer
                        shield-meter-x
                        (- (- (window-height window) 50)
                           shield-meter-height)
                        40
                        shield-meter-height
                        shield-meter-color))

  (let ((energy-meter-height (* (/ (player-energy-amount player) 100) meter-height))
        (energy-meter-x (- (window-width window) 135)))
    (render-text renderer
                 small-font
                 "E"
                 (+ energy-meter-x 11)
                 (- (- (window-height window) 70)
                    meter-height))
    (renderer-draw-rect renderer
                        energy-meter-x
                        (- (- (window-height window) 50)
                           energy-meter-height)
                        40
                        energy-meter-height
                        energy-meter-color))

  ;; Draw the score
  (render-text renderer
               small-font
               (string-append "Score: " (number->string (game-score-current game)))
               (- (window-width window) 210)
               (- (window-height window) 420))

  (render-text renderer
               small-font
               (string-append "Time: " (number->string (game-level-time game)))
               (- (window-width window) 210)
               (- (window-height window) 470)))

;;; -- Input --

(define (handle-prompt-input input-event)
  (if (input-event-key-down? input-event)
      (if (equal? (input-event-key-code input-event) input-key-code-y)
          (set! current-game (initialize-game))
          (if (equal? (input-event-key-code input-event) input-key-code-n)
              (begin
                (set! current-updater menu-updater)
                (set! current-renderer menu-renderer))))))

(define (handle-input-events game)
  (let next-event ((input-event (input-event-take input-state)))
    (if input-event
        (begin
          (if (equal? (game-state current-game) 'playing)
              (handle-controller-input (game-controller current-game)
                                       input-event)
              (handle-prompt-input input-event))
          (next-event (input-event-take input-state))))))

;;; -- Siphoning --

(define siphon-sphere (make-sphere 0 0 player-size))
(define siphon-score-rate 50)

(define (siphon-energy game player enemies time-delta)
  (sphere-center-x-set! siphon-sphere (player-x player))
  (sphere-center-y-set! siphon-sphere (player-y player))

  (let next-enemy ((enemies enemies))
    (if (pair? enemies)
        (begin
          (if (sphere-intersect? siphon-sphere
                                 (enemy-sphere (car enemies)))
              ;; Add score for siphoning
              (game-score-total-set! game (+ (game-score-total game)
                                             (* siphon-score-rate time-delta)))

              ;; TODO: Render indicator of siphoned enemy?
              (player-energy-amount-set! player
                                         (min 100
                                              (+ (player-energy-amount player)
                                                 (* (enemy-siphon-rate (car enemies))
                                                    time-delta)))))
          (next-enemy (cdr enemies))))))

;;; -- Game Loop --

(define current-game (initialize-game))

(define (game-updater time-delta)
  (if (not (window-needs-close? window))
      (begin
        ;; If we're still playing, update game entities
        (if (equal? (game-state current-game) 'playing)
            (begin
              ;; Count how much time we've been in the level
              (game-level-time-set! current-game
                                    (+ (game-level-time current-game)
                                       time-delta))

              ;; Update the player
              (handle-input-events current-game)
              (update-player (game-player current-game)
                             (game-controller current-game)
                             (lambda (bullet)
                               (game-bullets-set! current-game
                                                  (cons bullet
                                                        (game-bullets current-game))))
                             time-delta)

              (if (not (equal? (player-state (game-player current-game))
                               'alive))
                  (end-game current-game (player-state (game-player current-game))))

              ;; Update bullets
              (game-bullets-set! current-game
                                 (update-bullets (game-bullets current-game)
                                                 time-delta))

              ;; Spawn and update enemies
              (game-enemies-set! current-game
                                 (spawn-enemies (game-enemies current-game)
                                                (game-level-time current-game)
                                                max-level-time
                                                time-delta
                                                window))
              (game-enemies-set! current-game
                                 (update-enemies (game-enemies current-game)
                                                 (lambda (bullet)
                                                   (game-bullets-set! current-game
                                                                      (cons bullet
                                                                            (game-bullets current-game))))
                                                 time-delta
                                                 window))

              ;; Siphon energy if necessary
              (if (controller-siphon (game-controller current-game))
                  (siphon-energy current-game
                                 (game-player current-game)
                                 (game-enemies current-game)
                                 time-delta))

              ;; Check for collisions
              (check-collisions (game-player current-game)
                                (game-enemies current-game)
                                (game-bullets current-game)
                                (lambda (enemy)
                                  ;; Give the player some score
                                  (game-score-total-set! current-game
                                                         (+ (game-score-total current-game)
                                                            (enemy-score enemy)))))

              ;; Update the HUD
              (update-hud current-game time-delta))

            ;; Respond to input to the prompt
            (handle-input-events current-game))

        ;; Make sure we don't exit the game loop
        t)))

(define you-died-text "YOU DIED")
(define you-died-width (font-text-width result-font you-died-text))

(define depleted-text "DEPLETED")
(define depleted-width (font-text-width result-font depleted-text))

(define prompt-text "Play Again? (Y/N)")
(define prompt-width (font-text-width prompt-font prompt-text))

(define result-text nil)
(define result-width nil)

(define (game-renderer renderer)
  ;; Render the bullets
  (render-bullets renderer (game-bullets current-game))

  ;; Render the player's ship
  (render-player renderer (game-player current-game))

  ;; Render the enemies
  (render-enemies renderer (game-enemies current-game))

  ;; Render the HUD
  (render-hud renderer
              current-game
              (game-player current-game)
              window)

  ;; Display the result string
  (if (not (equal? (game-state current-game)
                   'playing))
      (begin
        (render-text renderer
                     result-font
                     result-text
                     (- (/ (- (window-width window) hud-width) 2)
                        (/ result-width 2))
                     500)
        (render-text renderer
                     prompt-font
                     prompt-text
                     (- (/ (- (window-width window) hud-width) 2)
                        (/ result-width 2))
                     650)))

  ;; Render the FPS
  (let ((fps (current-fps)))
    (if fps
        (render-text renderer
                     small-font
                     (string-append "FPS: " (number->string fps 3))
                     (- (window-width window) 220)
                     60))))

(define title-text "Silent Syphon")
(define title-width (font-text-width result-font title-text))

(define start-text "Press Space to Start")
(define start-width (font-text-width prompt-font start-text))

(define quit-text "Q to Quit")
(define quit-width (font-text-width prompt-font quit-text))

(define (menu-updater time-delta)
  (let next-event ((input-event (input-event-take input-state)))
    (if input-event
        (if (input-event-key-down? input-event)
            (if (equal? (input-event-key-code input-event)
                        input-key-code-space)
                (begin
                  (set! current-updater game-updater)
                  (set! current-renderer game-renderer))
                (not (equal? (input-event-key-code input-event)
                             input-key-code-q)))
            (next-event (input-event-take input-state)))
        t)))

(define (menu-renderer renderer)
  (render-text renderer
               result-font
               title-text
               (- (/ (window-width window) 2)
                  (/ title-width 2))
               150)

  (render-text renderer
               prompt-font
               start-text
               (- (/ (window-width window) 2)
                  (/ start-width 2))
               275)

  (render-text renderer
               prompt-font
               quit-text
               (- (/ (window-width window) 2)
                  (/ quit-width 2))
               350)

  (let next-line ((y 400)
                  (lines
                   (list "Instructions:"
                         "WSAD moves your ship"
                         "J siphons energy / K fires the weapon"
                         "All actions drain energy, try not to deplete!"
                         ""
                         "(C) 2022 By David Wilson / Flux Harmonic")))
    (if (pair? lines)
        (begin
          (render-text renderer
                       small-font
                       (car lines)
                       200
                       y)
          (next-line (+ y 50)
                     (cdr lines))))))

(define current-updater menu-updater)
(define current-renderer menu-renderer)


;; This will run the game functions until the updater returns nil
(run-engine-loop :renderer renderer
                 :update-func (lambda (time-delta)
                                (if (not (window-needs-close? window))
                                    (current-updater time-delta)))
                 :render-func (lambda (renderer)
                                ;; Clear the screen
                                (renderer-clear renderer 0 0 0)
                                (current-renderer renderer)))
